# Contributor: Nguyá»…n Quang Minh <minhnbnt at gmail dot com>

pkgname=pyrefly-bin
pkgver=0.38.0 # datasource=github-tags depName=facebook/pyrefly
pkgrel=1
pkgdesc='A faster Python type checker written in Rust'
arch=('aarch64' 'armv7h' 'x86_64')
url='https://pyrefly.org/'
license=('MIT')

_baseurl="https://meta.gallery.vsassets.io/_apis/public/gallery/publisher/\
meta/extension/pyrefly/${pkgver}/assetbyname/Microsoft.VisualStudio.Services.VSIXPackage"

source_aarch64=("pyrefly-aarch64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-arm64")
source_armv7h=("pyrefly-armv7h-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-armhf")
source_x86_64=("pyrefly-x86_64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-x64")

sha256sums_aarch64=('40e5194fcce6bb8b79d269631754853fad95fe8d12a1871b7b5bcfb31dd4ed5a')
sha256sums_armv7h=('2296212d4948611f5a0cb2925f075643a0cc18c16d4d6c559a98d37eb648aaef')
sha256sums_x86_64=('8e26d8511b1294523321149f7e7bac25b6803de9288168b406124d8b9d24f64b')

package() {

	cd "${srcdir}/extension"

	install -Dm755 "bin/pyrefly" "${pkgdir}/usr/bin/pyrefly"
	install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
