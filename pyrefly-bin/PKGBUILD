# Contributor: Nguyá»…n Quang Minh <minhnbnt at gmail dot com>

pkgname=pyrefly-bin
pkgver=0.32.0 # datasource=github-tags depName=facebook/pyrefly
pkgrel=1
pkgdesc='A faster Python type checker written in Rust'
arch=('aarch64' 'armv7h' 'x86_64')
url='https://pyrefly.org/'
license=('MIT')

_baseurl="https://meta.gallery.vsassets.io/_apis/public/gallery/publisher/\
meta/extension/pyrefly/${pkgver}/assetbyname/Microsoft.VisualStudio.Services.VSIXPackage"

source_aarch64=("pyrefly-aarch64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-arm64")
source_armv7h=("pyrefly-armv7h-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-armhf")
source_x86_64=("pyrefly-x86_64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-x64")

sha256sums_aarch64=('bdbae03e69ca3264769797b1a9a27a7a50f0f9b8bbae826a7e2a18fa5c792964')
sha256sums_armv7h=('60e82531e28a1efb359ca120910952dce5cbe7ad0266e0dde41cb25714e88f26')
sha256sums_x86_64=('a1f9275fc6aa926a2c5be6002c4631d14494e46b54d3def4356146019a14af17')

package() {

	cd "${srcdir}/extension"

	install -Dm755 "bin/pyrefly" "${pkgdir}/usr/bin/pyrefly"
	install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
