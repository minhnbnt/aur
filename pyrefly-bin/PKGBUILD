# Contributor: Nguyá»…n Quang Minh <minhnbnt at gmail dot com>

pkgname=pyrefly-bin
pkgver=0.27.0 # datasource=github-tags depName=facebook/pyrefly
pkgrel=1
pkgdesc='A faster Python type checker written in Rust'
arch=('aarch64' 'armv7h' 'x86_64')
url='https://pyrefly.org/'
license=('MIT')

_baseurl="https://meta.gallery.vsassets.io/_apis/public/gallery/publisher/\
meta/extension/pyrefly/${pkgver}/assetbyname/Microsoft.VisualStudio.Services.VSIXPackage"

source_aarch64=("pyrefly-aarch64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-arm64")
source_armv7h=("pyrefly-armv7h-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-armhf")
source_x86_64=("pyrefly-x86_64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-x64")

sha256sums_aarch64=('e5084fb03dd4739329391ff33739d2e357e80f49cda7592958e4b6481d8ab299')
sha256sums_armv7h=('9a07384bcc532ad88c4f87bbff7636256837b8d986f393d4653a1651bf416a0e')
sha256sums_x86_64=('d821d15a3bcb80bb788c67276db2056bdeb77d955d8131d0977520db921a30b4')

package() {

	cd "${srcdir}/extension"

	install -Dm755 "bin/pyrefly" "${pkgdir}/usr/bin/pyrefly"
	install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
