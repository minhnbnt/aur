# Contributor: Nguyá»…n Quang Minh <minhnbnt at gmail dot com>

pkgname=pyrefly-bin
pkgver=0.26.0 # datasource=github-tags depName=facebook/pyrefly
pkgrel=1
pkgdesc='A faster Python type checker written in Rust'
arch=('aarch64' 'armv7h' 'x86_64')
url='https://pyrefly.org/'
license=('MIT')

_baseurl="https://meta.gallery.vsassets.io/_apis/public/gallery/publisher/\
meta/extension/pyrefly/${pkgver}/assetbyname/Microsoft.VisualStudio.Services.VSIXPackage"

source_aarch64=("pyrefly-aarch64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-arm64")
source_armv7h=("pyrefly-armv7h-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-armhf")
source_x86_64=("pyrefly-x86_64-${pkgver}.vsix::${_baseurl}?targetPlatform=linux-x64")

sha256sums_aarch64=('20f1c8f111bb1622daaad5f6e0f1c3636d38bd58b5ed4710db098d1969696035')
sha256sums_armv7h=('1a1b3697c937edd69daf068243808810019f6dc335c09f20dd8bd0a6972b8ab9')
sha256sums_x86_64=('68d91c9599a1d8fd2a3af537a2f26e4e12eec43bae794b5cbe1600e8d9bb59f2')

package() {

	cd "${srcdir}/extension"

	install -Dm755 "bin/pyrefly" "${pkgdir}/usr/bin/pyrefly"
	install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
